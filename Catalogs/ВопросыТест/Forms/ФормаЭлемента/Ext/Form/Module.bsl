
&НаКлиенте
Процедура ВыбратьФайлКартинки(Команда)
	// Создаем оповещение, именно процедура "ОбработатьВыборФайла" будет вызвана при закрытии окна выбора файла 
	Оповещение = Новый ОписаниеОповещения("ОбработатьВыборФайла",ЭтотОбъект);
	
	//Открыть интерактивно окна для выбора файла
	//Метод НачатьПомещениеФайла помещает выбранный файл во временное хранилище (если выбор файла был произведен) и вызывает процедуру "ОбработатьВыборФайла" в этом же модуле.
	//"УникальныйИдентификатор", этим свойством мы связали временное хранилище с текущей формой. В противном случае файл в хранилище помещен будет, 
	//но само хранилище при серверном вызовет будет уничтожено, а нам оно еще понадобится, для записи файла в реквизит справочника.
	НачатьПомещениеФайла(Оповещение,,,Истина, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборФайла(Результат,Адрес,ВыбранноеИмяФайла,ДополнительныеПараметры)   Экспорт
	Если Не Результат Тогда 
	 	Возврат
	КонецЕсли;
	// Если пользователь не отказался от выбора файла, то в реквизит формы "СсылкаНаКартинку" помещаем адрес во временном хранилище, куда были помещены данные картинки.
	СсылкаНаКартинку = Адрес;
КонецПроцедуры	


&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЭтоАдресВременногоХранилища(СсылкаНаКартинку) Тогда
		// "ТекущийОбъект" - это сконвертированные данные формы, которые пишутся в базу в этот момент времени.
		ТекущийОбъект.ДанныеКартинки = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(СсылкаНаКартинку));
	КонецЕсли;
	
	Если ЭтоАдресВременногоХранилища(СсылкаНаКартинкуОтвет) Тогда
		ТекущийОбъект.ДанныеКартинкиОтвет = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(СсылкаНаКартинкуОтвет));
	КонецЕсли;	
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//открытие формы, для отображения картинки уже сохраненной в реквизите элемента справочника.
	СсылкаНаКартинку = ПолучитьНавигационнуюСсылку(Объект.Ссылка,"ДанныеКартинки");
	Элементы.СсылкаНаКартинку.РазмерКартинки = РазмерКартинки.Пропорционально;
	СсылкаНаКартинкуОтвет = ПолучитьНавигационнуюСсылку(Объект.Ссылка,"ДанныеКартинкиОтвет");
	Элементы.СсылкаНаКартинкуОтвет.РазмерКартинки = РазмерКартинки.Пропорционально;
КонецПроцедуры


&НаКлиенте
Процедура ВыбратьФайлКартинкиОтвет(Команда)
	Оповещение = Новый ОписаниеОповещения("ОбработатьВыборФайлаОтвет",ЭтотОбъект);
	НачатьПомещениеФайла(Оповещение,,,Истина, УникальныйИдентификатор);
КонецПроцедуры


&НаКлиенте
Процедура ОбработатьВыборФайлаОтвет(Результат,Адрес,ВыбранноеИмяФайла,ДополнительныеПараметры)   Экспорт
	Если Не Результат Тогда 
	 	Возврат
	КонецЕсли;
	// Если пользователь не отказался от выбора файла, то в реквизит формы "СсылкаНаКартинку" помещаем адрес во временном хранилище, куда были помещены данные картинки.
	СсылкаНаКартинкуОтвет = Адрес;
	Модифицированность = Истина;
	Объект.ОтветКартинкаЗагружен = Истина;
КонецПроцедуры	

